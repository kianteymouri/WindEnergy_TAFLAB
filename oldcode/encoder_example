/*!
 * @file AS5600_basic.ino
 *
 * Basic example for the Adafruit AS5600 library
 *
 * Written by Limor Fried for Adafruit Industries.
 * MIT license, all text above must be included in any redistribution
 */

#include <Adafruit_AS5600.h>

//Instantiate AS5600 instance
Adafruit_AS5600 as5600;

void setup() {
  Serial.begin(115200);
  while (!Serial)
    delay(10);

  Serial.println("Adafruit AS5600 Basic Test");

  if (!as5600.begin()) {
    Serial.println("Could not find AS5600 sensor, check wiring!");
    while (1)
      delay(10);
  }

  Serial.println("AS5600 found!");


  as5600.enableWatchdog(false);
  // Normal (high) power mode
  as5600.setPowerMode(AS5600_POWER_MODE_NOM);
  // No Hysteresis
  as5600.setHysteresis(AS5600_HYSTERESIS_OFF);

  // analog output
  as5600.setOutputStage(AS5600_OUTPUT_STAGE_ANALOG_FULL);

  // OR can do pwm!
  // as5600.setOutputStage(AS5600_OUTPUT_STAGE_DIGITAL_PWM);
  // as5600.setPWMFreq(AS5600_PWM_FREQ_920HZ);

  // setup filters
  as5600.setSlowFilter(AS5600_SLOW_FILTER_2X);
  as5600.setFastFilterThresh(AS5600_FAST_FILTER_THRESH_SLOW_ONLY);

  // Reset position settings to defaults
  as5600.setZPosition(0);
  as5600.setMPosition(4095);
  as5600.setMaxAngle(4095);

  Serial.println("Waiting for magnet detection...");
}

float rawAngle = 0;
float angle = 0;
float prevAngle = 0;
float diff = 0;
uint16_t rate = 10;
bool firstReading = true;  // Add this flag

void loop() {
  while(true){
      if (!as5600.isMagnetDetected()) {
        firstReading = true;  // Reset flag when magnet lost
        return;
      }

      rawAngle = as5600.getRawAngle() * 0.087890625;
      angle = as5600.getAngle() * 0.087890625;
      
      if (firstReading) {
        prevAngle = angle;  // Initialize prevAngle on first reading
        firstReading = false;
        delay(10);
        continue;  // Skip this iteration, no valid speed yet
      }
      
      diff = angle - prevAngle;
      if (diff < -180){
        diff += 360;
      } else if (diff > 180){
        diff -= 360;
      }
      
      prevAngle = angle;  // Update for next iteration
    
      Serial.print("Angle Orientation (deg): ");
      Serial.print(angle);
      Serial.print(" | Rotational Speed (deg/s): ");
      Serial.print(diff / rate * 1000);
      Serial.print(" | Rotational Speed (rpm): ");
      Serial.print(diff / rate / 60 * 1000);

      if (as5600.isAGCminGainOverflow()) {
        Serial.print(" | MH: magnet too strong");
      }
      if (as5600.isAGCmaxGainOverflow()) {
        Serial.print(" | ML: magnet too weak");
      }

      Serial.println();
      delay(50);
  }
}
